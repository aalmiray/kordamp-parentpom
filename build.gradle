/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018-2019 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'groovy-base'
    id 'org.kordamp.gradle.groovy-project' version '0.30.0'
    id 'org.kordamp.gradle.plugin'         version '0.30.0'
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = '**undefined**'
if (!project.hasProperty('bintrayApiKey'))   ext.bintrayApiKey   = '**undefined**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name          = 'Kordamp parent POM'
        description   = 'Parent POM for Kordamp projects'
        vendor        = 'Kordamp'
        inceptionYear = '2018'
        tags          = ['kordamp', 'pom']

        links {
            website      = 'https://github.com/aalmiray/kordamp-parentpom'
            issueTracker = 'https://github.com/aalmiray/kordamp-parentpom/issues'
            scm          = 'https://github.com/aalmiray/kordamp-parentpom.git'
        }

        specification  { enabled = false }

        implementation { enabled = false }

        people {
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'stagingRelease'
                url  = "${project.rootProject.buildDir}/repos/staging/release"
            }
            repository {
                name = 'stagingSnapshot'
                url  = "${project.rootProject.buildDir}/repos/staging/snapshot"
            }
        }
    }

    groovydoc {
        replaceJavadoc = true
        options {
            link 'https://docs.gradle.org/5.0/javadoc/', 'org.gradle.'
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        enabled      = true
        repo         = 'kordamp'
        userOrg      = 'aalmiray'
        name         = rootProject.name
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
    }

    publishing {
        signing             = false
        releasesRepository  = 'stagingRelease'
        snapshotsRepository = 'stagingSnapshot'
    }

    plugin {
        enabled             = true
        id                  = project.pluginId
        implementationClass = 'org.kordamp.gradle.KordampParentPomPlugin'
    }
}

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}

repositories {
    jcenter()
    gradlePluginPortal()
}

dependencies {
    compile gradleApi()

    compile 'org.kordamp.gradle:java-project-gradle-plugin:0.30.0'
}

task publishRelease(dependsOn: [bintrayUpload, publishPlugins]) {}
