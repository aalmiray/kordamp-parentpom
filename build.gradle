/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'org.kordamp.gradle.project'  version '0.9.0'
    id 'org.kordamp.gradle.bintray'  version '0.9.0'
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = '**undefined**'
if (!project.hasProperty('bintrayApiKey'))   ext.bintrayApiKey   = '**undefined**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name          = 'kordamp-parentpom'
        description   = 'Parent POM for kordamp projects'
        vendor        = 'Kordamp'
        inceptionYear = '2018'

        links {
            website      = 'https://github.com/aalmiray/kordamp-parentpom'
            issueTracker = 'https://github.com/aalmiray/kordamp-parentpom/issues'
            scm          = 'https://github.com/aalmiray/kordamp-parentpom.git'
        }

        specification  { enabled = false }

        implementation { enabled = false }

        people {
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                roles = ['developer']
            }
        }
    }

    groovydoc {
        replaceJavadoc = true
        options {
            link 'https://docs.gradle.org/4.0/javadoc/', 'org.gradle.'
        }
    }

    license {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        enabled = true
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
        repo    = 'kordamp'
        userOrg = 'aalmiray'
        name    = rootProject.name
    }
}

repositories {
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
    compile gradleApi()

    compile 'org.kordamp.gradle:project-gradle-plugin:0.9.0'
    compile 'org.kordamp.gradle:bintray-gradle-plugin:0.9.0'
}

gradlePlugin {
    plugins {
        parentpomPlugin {
            id                  = project.pluginId
            implementationClass = 'com.acme.gradle.KordampParentPomPlugin'
        }
    }
}

bintray {
    publications += ['parentpomPluginPluginMarkerMaven', 'pluginMaven']
    pkg {
        version {
            attributes = ['gradle-plugin': "${project.pluginId}:${project.group}:${project.name}"]
        }
    }
}
